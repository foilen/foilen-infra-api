plugins {
  id "com.github.ben-manes.versions" version "$versionsVersion"
  id 'io.spring.dependency-management' version "$springDependencyManagementVersion"
}

apply plugin: 'eclipse'
apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'signing'

group = 'com.foilen'
version = System.getenv('VERSION')?:'master-SNAPSHOT'
ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

repositories {
  mavenLocal()
  mavenCentral()
}

sourceCompatibility = 11

dependencyManagement {
  imports {
    mavenBom "org.springframework.boot:spring-boot-starter-parent:$springBootVersion"
  }
}

eclipse {
  classpath {
    downloadSources = true
    downloadJavadoc = true
  }
}

dependencies {

  api "com.fasterxml.jackson.core:jackson-databind"
  api "com.foilen:jl-smalltools:$smalltoolsVersion"
  api "com.google.guava:guava:$guavaVersion"
  api "javax.xml.bind:jaxb-api"
  api "org.apache.commons:commons-lang3"
  api "org.slf4j:slf4j-api"
  
  // Spring
  api "org.apache.httpcomponents:httpclient"
  api "org.springframework:spring-context"
  api "org.springframework:spring-web"
  
  // Test
  testCompileOnly "junit:junit"
  testImplementation "org.junit.jupiter:junit-jupiter-api"
  testRuntimeOnly "org.junit.vintage:junit-vintage-engine"
  testImplementation 'org.springframework.data:spring-data-jpa'
  testImplementation 'ch.qos.logback:logback-classic'
}

java {
  withJavadocJar()
  withSourcesJar()
}

publishing {
  repositories {
    maven {
      url = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
      credentials {
        username = System.getenv('OSSRH_USER')
        password = System.getenv('OSSRH_PASS')
      }
    }
  }

  publications {
    mavenJava(MavenPublication) {
      pom {
        name = "foilen-infra-api"
        description = 'This is the infra service API'
        url = 'https://github.com/foilen/foilen-infra-api'
        from components.java
        
        scm {
          connection = "scm:git:git@github.com:foilen/foilen-infra-api.git"
          url = "https://github.com/foilen/foilen-infra-api"
        }

        licenses {
          license {
            name = "MIT"
            url = "https://opensource.org/licenses/MIT"
          }
        }

        developers {
          developer {
            id = 'foilen'
            name = 'Simon Levesque'
            email = 'simon@foilen.com'
          }
        }
      }
    }
  }
}

signing {
  sign publishing.publications.mavenJava
}
tasks.withType(Sign) {
  onlyIf { isReleaseVersion }
}
